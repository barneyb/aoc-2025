fn free(void* ptr);

struct ArrayList;
struct StringBuilder;

# I return an `ArrayList` containing substrings of `str`, delimited by `sep`.
# If `sep` is the empty string, panic.
pub fn strsplit(char* str, char* sep) {
    int len = strlen(sep);
    if len == 0 { panic(4, "empty sep to strsplit\n", 22); }
    ArrayList* parts = ArrayList__new_owned(10, &free);
    char* ptr = strstr(str, sep);
    while ptr != null {
        parts.push(substr(str, 0, ptr - str));
        str = ptr + len;
        ptr = strstr(str, sep);
    }
    parts.push(substr(str, 0, strlen(str)));
    return parts;
}

# I return an `ArrayList` containing owned substrings of `str`, separated by
# `sep`. If `sep` is the empty string, panic.
pub fn strsplitafter(char* str, char* sep) {
    int len = strlen(sep);
    if len == 0 { panic(4, "empty sep to strsplitafter\n", 27); }
    ArrayList* parts = ArrayList__new_owned(10, &free);
    char* ptr = strstr(str, sep);
    while ptr != null {
        parts.push(substr(str, 0, ptr - str + len));
        str = ptr + len;
        ptr = strstr(str, sep);
    }
    parts.push(substr(str, 0, strlen(str)));
    return parts;
}

pub fn strjoin(ArrayList* parts, char* sep) {
    int l = parts.size();
    if l == 0 { return ""; }
    StringBuilder* sb = StringBuilder__new(l);
    sb.push_str(parts.get(0));
    int i = 1;
    while i < l {
        sb.push_str(sep);
        sb.push_str(parts.get(i));
        i = i + 1;
    }
    return sb.into_chars();
}

# I return a pointer to the first instance of `substr` within `str`, or null if
# there are no instances. If `substr` is the empty string, `str` is returned.
pub fn strstr(char* str, char* substr) {
    # if substr is empty, it matches the start of str
    char first_char = *substr;
    if first_char == null { return str; }

    # otherwise, search
    char curr = *str;
    while curr != null {
        if curr == first_char {
            # the first chars matched, check the rest
            char* lhs = str;
            char* rhs = substr;
            char l = *lhs;
            char r = *rhs;
            while true {
                if r == null {
                    # found it!
                    return str;
                }
                if l != r {
                    done;
                }
                lhs = lhs + 1;
                rhs = rhs + 1;
                l = *lhs;
                r = *rhs;
            }
        }
        str = str + 1;
        curr = *str;
    }

    # didn't find it
    return null;
}
