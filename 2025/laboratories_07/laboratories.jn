pub fn main() {
    int split_count = 0;
    int* beams = null;
    int width;
    while !iseof() {
        char* line = read_line();
        free(read_line()); # throw away the all-dots lines
        if beams == null {
            # first line: find the S, and start the beam
            width = strlen(line);
            beams = calloc(width, sizeof(int));
            int i = strchr(line, 'S') - line;
#            printf("Found 'S' at %d\n", i);
            beams[i] = 1;
        } else {
            int* next = calloc(width, sizeof(int));
            i = width;
            while i > 0 {
                i = i - 1;
                if beams[i] == 0 { again; }
                if line[i] == '^' {
                    split_count = split_count + 1;
                    # each beam continues both left and right
                    next[i-1] = next[i-1] + beams[i];
                    next[i+1] = next[i+1] + beams[i];
                } else {
                    # each beam just continues downward
                    next[i] = next[i] + beams[i];
                }
            }
            free(beams);
            beams = next;
        }
#        printf("Worlds: %14d\n", get_world_count(beams, width));
        free(line);
    }
    printf("Split Count: %d\n", split_count);
    aocd_verify_a("%d", split_count);
    int world_count = get_world_count(beams, width);
    free(beams);
    printf("World Count: %d\n", world_count);
    aocd_verify_b("%d", world_count);
}

fn get_world_count(int* beams, int width) {
    int world_count = 0;
    int i = width;
    while i > 0 {
        i = i - 1;
        world_count = world_count + beams[i];
    }
    return world_count;
}
