pub fn main() {
    aocd_run(&solve);
}

fn solve(char** ans_a, char** ans_b) {
    int part_a = 0;
    int part_b = 0;
    while !iseof() {
        char* l = read_line();
        int len = strlen(l);
        int j = max_joltage(l, len, 2);
        part_a = part_a + j;
        j = max_joltage(l, len, 12);
        part_b = part_b + j;
        free(l);
    }
    *ans_a = itoa(part_a);
    *ans_b = itoa(part_b);
}

fn max_joltage(char* bank, int to_check, int to_energize) {
    if to_energize == 0 { return 0; }
    to_energize = to_energize - 1;
    int a = idx_of_largest_digit(bank, to_check - to_energize);
    int next = a + 1;
    return (bank[a] - '0') * pow(10, to_energize)
        + max_joltage(bank + next, to_check - next, to_energize);
}

fn idx_of_largest_digit(char* str, int len) {
    int idx = 0;
    int i = 1;
    while i < len {
        if str[i] > str[idx] {
            idx = i;
            # can't be larger than 9
            if str[i] == '9' {
                done;
            }
        }
        i = i + 1;
    }
    return idx;
}
