struct Range {
    int lo,
    int hi,
}

fn Range__new(int lo, int hi) {
    if hi < lo {
        printf("hi cannot be less than lo; %d-%d is illegal?!\n", lo, hi);
        exit(1);
    }
    Range* r = malloc(sizeof(Range));
    r.lo = lo;
    r.hi = hi;
    return r;
}

fn Range_contains(Range* self, int n) {
    return n >= self.lo && n <= self.hi;
}

fn Range_size(Range* self) {
    return self.hi - self.lo + 1;
}

fn Range_cmp(Range* a, Range* b) {
    if a.lo == b.lo {
        return a.hi - b.hi;
    }
    return a.lo - b.lo;
}

pub fn main() {
    aocd_run(&solve);
}

fn solve(char** ans_a, char** ans_b) {
    int start = micro_time();
    ArrayList* ranges = read_ranges();

#    dump_ranges(ranges);
    ranges.sort(&Range_cmp);
#    dump_ranges(ranges);
    stitch_ranges(ranges);
    printf("%d us : register ranges\n", micro_time() - start);
#    dump_ranges(ranges);

    start = micro_time();
    *ans_a = itoa(part_a(ranges));
    printf("%d us : part_a\n", micro_time() - start);
    start = micro_time();
    *ans_b = itoa(part_b(ranges));
    printf("%d us : part_b\n", micro_time() - start);

    ranges.drop();
}

fn read_ranges() {
    ArrayList* ranges = ArrayList__new_owned(10, &free);

    # read in the ranges
    while !iseof() {
        char* line = read_line();
        if *line == null {
            free(line);
            done;
        }
        ranges.push(parse_range(line));
        free(line);
    }

    return ranges;
}

fn stitch_ranges(ArrayList* ranges) {
    ArrayList* stack = ArrayList__new(ranges.size());
    while ranges.size() > 0 {
        stack.push(ranges.pop());
    }
    Range* prev = stack.pop();
    ranges.push(prev);
    while stack.size() > 0 {
        Range* curr = stack.pop();
        if prev.contains(curr.lo) {
            if curr.hi > prev.hi {
                prev.hi = curr.hi;
            }
            free(curr);
        } else {
            prev = curr;
            ranges.push(prev);
        }
    }
    stack.drop();
}

fn part_a(ArrayList* ranges) {
    # read in the ids
    int count = 0;
    while !iseof() {
        char* line = read_line();
        int id = atoi(line);
        int lo = 0;
        int hi = ranges.size() - 1;
        while lo <= hi {
            int mid = lo + ((hi-lo) >> 2);
            Range* r = ranges.get(mid);
            if r.hi < id {
                lo = mid + 1;
            } else if r.lo > id {
                hi = mid - 1;
            } else {
                # got it
                count = count + 1;
                done;
            }
        }
        free(line);
    }
    printf("Fresh Ingredients: %d\n", count);
    return count;
}

fn part_b(ArrayList* ranges) {
    int count = 0;
    int i = ranges.size();
    while i > 0 {
        i = i - 1;
        Range* r = ranges.get(i);
        count = count + r.size();
    }
    printf("Fresh IDs        : %d\n", count);
    return count;
}

fn dump_ranges(ArrayList* ranges) {
    int len = ranges.size();
    printf("Ranges (%d):\n", len);
    int i = 0;
    while i < len {
        Range* r = ranges.get(i);
        i = i + 1;
        printf("  %d - %d\n", r.lo, r.hi);
    }
}

fn parse_range(char* str) {
    int lo = read_int(&str);
    str = str + 1; # dash
    return Range__new(lo, read_int(&str));
}

fn read_int(char** str) {
    int i = 0;
    char* s = *str;
    while isdigit(*s) {
        i = i * 10 + (*s - '0');
        s = s + 1;
    }
    *str = s;
    return i;
}
