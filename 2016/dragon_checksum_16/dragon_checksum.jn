struct StringBuilder;

pub fn main(int argc, char** argv) {
    if argc == 1 {
        # the real deal
        char* init = read_line();
        char* a = into_checksum(strclone(init), 272);
        aocd_verify_a("%s", a);
        free(a);
        char* b = into_checksum(init, 35651584);
        aocd_verify_b("%s", b);
        free(b);
    } else if argc == 3 {
        int disk_size = atoi(argv[1]);
        char* data = argv[2];
        data = into_checksum(strclone(data), disk_size);
        printf("  checksum: '%s'\n", data);
        free(data);
    } else {
        eprintf("Expected zero or two arguments\n");
        exit(1);
    }
}

fn into_checksum(char* data, int disk_size) {
    printf("For input '%s', fill %d-byte disk\n", data, disk_size);
    int len = strlen(data);
    StringBuilder* sb;
    int i;
    while len < disk_size {
        sb = StringBuilder__new(disk_size + 2);
        sb.push_str(data);
        sb.push('0');
        i = len - 1;
        while i >= 0 {
            if data[i] == '0' {
                sb.push('1');
            } else {
                sb.push('0');
            }
            i = i - 1;
        }
        free(data);
        len = sb.length();
        data = sb.into_chars();
    }
#    printf("  data: '%s'\n", data);
    data[disk_size] = null;
    len = strlen(data);
    while true {
        sb = StringBuilder__new(len / 2 + 1);
        i = 0;
        while i < len {
            if data[i] == data[i + 1] {
                sb.push('1');
            } else {
                sb.push('0');
            }
            i = i + 2;
        }
        free(data);
        len = sb.length();
        data = sb.into_chars();
        if len % 2 == 1 { done; }
    }
    return data;
}
